cmake_minimum_required(VERSION 2.8)

project(rlite)

# Compiler checks
message(STATUS "C Compiler: ${CMAKE_C_COMPILER}")
message(STATUS "C++ Compiler: ${CMAKE_CXX_COMPILER}")

if(CMAKE_C_COMPILER STREQUAL "CMAKE_C_COMPILER-NOTFOUND")
	message(FATAL_ERROR "Cannot find C compiler")
endif()

if(CMAKE_CXX_COMPILER STREQUAL "CMAKE_CXX_COMPILER-NOTFOUND")
	message(FATAL_ERROR "Cannot find C++ compiler")
endif()

# Some globbing and headers include
set(INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/include)

file(GLOB RINA_HEADERS "include/rlite/*.h")
file(GLOB UIPCP_GPB_PROTOFILES "user/uipcp-gpb/*.proto")

message(STATUS "CMAKE_CURRENT_SOURCE_DIR: ${CMAKE_CURRENT_SOURCE_DIR}")
message(STATUS "INCLUDE_DIR: ${INCLUDE_DIR}")
message(STATUS "RINA HEADERS: ${RINA_HEADERS}")

add_definitions("-Wall")

include_directories(${INCLUDE_DIR})

# External dependencies
find_package(Threads REQUIRED)
find_package(Protobuf REQUIRED)

if(PROTOBUF_PROTOC_EXECUTABLE STREQUAL "PROTOBUF_PROTOC_EXECUTABLE-NOTFOUND")
	message(FATAL_ERROR "Cannot find protocol buffer compiler")
endif()

# Protocol buffer processing
protobuf_generate_cpp(CDAP_GPB_SRC CDAP_GPB_HDR user/CDAP.proto)
protobuf_generate_cpp(UIPCP_GPB_SRC UIPCP_GPB_HDR ${UIPCP_GPB_PROTOFILES})

# Libraries generated by the project
add_library(rlite SHARED user/rlite-utils.c user/rina-kernel-numtables.c user/rina-conf-numtables.c user/rlite-evloop.c user/pending_queue.c user/rlite-appl.c)
add_library(rlite-cdap SHARED user/cdap.cpp ${CDAP_GPB_SRC} ${CDAP_GPB_HDR})
add_library(rlite-conf SHARED user/rlite-conf.c)
add_library(uipcp-normal STATIC user/uipcp-normal.cpp user/uipcp-codecs.cpp user/uipcp-normal.hpp user/uipcp-enrollment.cpp user/uipcp-flow-allocation.cpp user/uipcp-application-registration.cpp user/uipcp-lower-flows.cpp user/uipcp-qos.cpp ${UIPCP_GPB_SRC} ${UIPCP_GPB_HDR})

target_link_libraries(rlite ${CMAKE_THREAD_LIBS_INIT})
target_link_libraries(rlite-cdap rlite ${PROTOBUF_LIBRARY})
target_link_libraries(rlite-conf rlite)
target_link_libraries(uipcp-normal rlite-cdap rlite-conf)

message(STATUS "Adding include dir ${CMAKE_CURRENT_BINARY_DIR} to rlite-cdap target")
target_include_directories(rlite-cdap PUBLIC ${CMAKE_CURRENT_BINARY_DIR})

# Executables generated by the project
add_executable(rinaperf user/rinaperf.c)
add_executable(rlite-uipcps user/uipcp-container.c user/uipcp-unix.c)
add_executable(rina-config user/rina-config.c)
add_executable(test-cdap user/test-cdap.cpp)

target_link_libraries(rinaperf rlite)
target_link_libraries(rlite-uipcps rlite rlite-conf uipcp-normal)
target_link_libraries(rina-config rlite rlite-conf)
target_link_libraries(test-cdap rlite-cdap)

# Installation directives
install(TARGETS rinaperf rlite-uipcps rina-config DESTINATION usr/bin)
install(TARGETS rlite rlite-cdap rlite-conf DESTINATION usr/lib)
install(FILES ${RINA_HEADERS} DESTINATION usr/include/rlite)
install(FILES user/uipcp-qoscubes.qos DESTINATION etc/rlite)
