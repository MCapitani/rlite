# See http://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# for more information on packaging from GIT sources.

# Maintainer: Vincenzo Maffione <v.maffione@gmail.com>
pkgname=rlite
pkgver=0.3
pkgrel=1
pkgdesc="Recursive InterNetwork Architecture user/kernel prototype written in C."
arch=('any')
url=()
license=('BSD')
groups=()
depends=('linux' 'glibc')
makedepends=('git' 'linux-headers')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install="rlite.install"
source=("rlite.install" "rlite.conf" "rlite.service")
noextract=()
md5sums=("047aa5adec4c52ddbf86d12dbf300f71" "76e6df2e7a96156829f702a42f000f42" "ecaab3e792c1c36048a069334337b380")

_gitroot="$PWD/.."
_gitname="rina"

build() {
    # Download the latest netmap code from the public repository
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
        cd "$srcdir/$_gitname"
        # We expect to be already on branch master
        git clone "$_gitroot/kernel/vmpi" "kernel/vmpi"
        # We want to be on the 3.18 branch
        cd kernel/vmpi
        git checkout 3.18
        cd ..
        cd ..
    fi
    msg "GIT checkout done or server timeout"

    RKVER=$(uname -r | sed 's|-.*||g')
    KMAJVER=$(echo "$RKVER" | sed 's|\.[0-9]\+$||g')
    msg "Building on kernel ${RKVER}..."

    cd "$srcdir/$_gitname"
    ./configure --with-vmpi || exit 1
    make || exit 1
    msg "Build complete"
}

package() {
    # Compute the version numbers of the running kernel
    KVER1=$(uname -r)
    KVER2=$(uname -r | sed 's/\.[0-9]\+-[0-9]\+//')

    # Install the rina kernel modules into the extramodules-VERSION directory
    mkdir -p "$pkgdir/usr/lib/modules/extramodules-${KVER2}"
    cp $srcdir/$_gitname/kernel/*.ko "$pkgdir/usr/lib/modules/extramodules-${KVER2}"
    cp $srcdir/$_gitname/kernel/vmpi/*.ko "$pkgdir/usr/lib/modules/extramodules-${KVER2}"

    # Install the userspace programs and libraries
    mkdir -p "$pkgdir/usr/bin"
    mkdir -p "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/librlite.so" "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/librlite-cdap.so" "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/librlite-conf.so" "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/rinaperf" "$pkgdir/usr/bin"
    cp "$srcdir/$_gitname/build/rlite-uipcps" "$pkgdir/usr/bin"
    cp "$srcdir/$_gitname/build/rina-config" "$pkgdir/usr/bin"

    # Install the rlite public headers
    mkdir -p "$pkgdir/usr/include/rlite"
    cp $srcdir/$_gitname/include/rlite/*.h "$pkgdir/usr/include/rlite"

    # Install configuration files
    mkdir -p "$pkgdir/etc/rlite"
    cp $srcdir/$_gitname/user/uipcp-qoscubes.qos "$pkgdir/etc/rlite"
    cp $srcdir/$_gitname/user/shim-inet4-dir "$pkgdir/etc/rlite"

    # Install other system files
    mkdir -p "$pkgdir/etc/systemd/system"
    mkdir -p "$pkgdir/etc/modules-load.d"
    cp $srcdir/$_gitname/archlinux/rlite.service "$pkgdir/etc/systemd/system"
    cp $srcdir/$_gitname/archlinux/rlite.conf "$pkgdir/etc/modules-load.d"
}

# vim:set ts=2 sw=2 et:
