file(GLOB UIPCP_GPB_PROTOFILES "uipcp-gpb/*.proto")

# External dependencies
find_package(Threads REQUIRED)
find_package(Protobuf REQUIRED)

if(PROTOBUF_PROTOC_EXECUTABLE STREQUAL "PROTOBUF_PROTOC_EXECUTABLE-NOTFOUND")
	message(FATAL_ERROR "Cannot find protocol buffer compiler")
endif()

# Protocol buffer processing
protobuf_generate_cpp(CDAP_GPB_SRC CDAP_GPB_HDR CDAP.proto)
protobuf_generate_cpp(UIPCP_GPB_SRC UIPCP_GPB_HDR ${UIPCP_GPB_PROTOFILES})

# Libraries generated by the project
add_library(rlite SHARED rlite-utils.c rina-kernel-numtables.c rina-conf-numtables.c rlite-evloop.c pending_queue.c rlite-appl.c)
add_library(rlite-cdap SHARED cdap.cpp ${CDAP_GPB_SRC} ${CDAP_GPB_HDR})
add_library(rlite-conf SHARED rlite-conf.c)
add_library(uipcp-normal STATIC uipcp-normal.cpp uipcp-normal-codecs.cpp uipcp-normal.hpp uipcp-normal-enroll.cpp uipcp-normal-flow-alloc.cpp uipcp-normal-appl-reg.cpp uipcp-normal-lower-flows.cpp uipcp-normal-qos.cpp ${UIPCP_GPB_SRC} ${UIPCP_GPB_HDR})

target_link_libraries(rlite ${CMAKE_THREAD_LIBS_INIT})
target_link_libraries(rlite-cdap rlite ${PROTOBUF_LIBRARY})
target_link_libraries(rlite-conf rlite)
target_link_libraries(uipcp-normal rlite-cdap rlite-conf)

message(STATUS "Adding include dir ${CMAKE_CURRENT_BINARY_DIR} to rlite-cdap target")
target_include_directories(rlite-cdap PUBLIC ${CMAKE_CURRENT_BINARY_DIR})

# Executables generated by the project
add_executable(rlite-uipcps uipcp-container.c uipcp-unix.c uipcp-shim-inet4.c)
add_executable(test-cdap test-cdap.cpp)

target_link_libraries(rlite-uipcps rlite rlite-conf uipcp-normal)
target_link_libraries(test-cdap rlite-cdap)

# Installation directives
install(TARGETS rlite-uipcps DESTINATION usr/bin)
install(TARGETS rlite rlite-cdap rlite-conf DESTINATION usr/lib)
install(FILES uipcp-qoscubes.qos shim-inet4-dir DESTINATION etc/rlite)
install(FILES cdap.hpp DESTINATION usr/include/rlite)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/CDAP.pb.h DESTINATION usr/include/rlite)

add_subdirectory(tools)
